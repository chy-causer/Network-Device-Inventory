#!/usr/bin/perl

use warnings FATAL => 'all';
use strict;
our $VERSION = '1.00';

use Carp;
use CGI;
use Config::Tiny;
use DBD::Pg;
use DBI;
use Log::Log4perl;
use Readonly;
use Template;

use Inventory 1.0;
use Inventory::Contracts 1.0;
use Inventory::Invoices 1.0;
use Inventory::Servicelevels 1.0;

my $HOSTNAME = CGI::url( -base => 1 );
my $BASEURL = $HOSTNAME;

# remove everything from the first . onwards
$HOSTNAME =~ s/\..*$//gx;

# remove everything from the start to the last /
$HOSTNAME =~ s/^.*\///gx;

my $CONFIG      = Config::Tiny->new();
my $CONFIG_FILE = "/srv/www/$HOSTNAME/lib/inventory.ini";
$CONFIG = Config::Tiny->read("$CONFIG_FILE");

my $dbhost      = $CONFIG->{'database'}->{db_host};
my $dbname      = $CONFIG->{'database'}->{db_name};
my $dbuser      = $CONFIG->{'database'}->{db_user};
my $dbpass      = $CONFIG->{'database'}->{db_pass};
my $dbh         = Inventory::dbconnect( $dbname, $dbuser, $dbhost, $dbpass );
my $valid_users = $CONFIG->{'general'}->{allowed_users};
my $htmltemplatepath  = $CONFIG->{'locations'}->{tt_dir};
my $localtemplatepath = $CONFIG->{'locations'}->{local_tt_dir};
my $logconfigfile     = $CONFIG->{'locations'}->{log_conf};

Log::Log4perl->init($logconfigfile);
my $logger = Log::Log4perl->get_logger();

Readonly my $PAGETITLE => 'Contracts';
Readonly my $SECTIONTITLE => 'admin';
Readonly my $PAGENAME     => $ENV{'SCRIPT_NAME'};

#############################################################################
#                                     main                                  #
#############################################################################

my $q = CGI->new();
print $q->header();

# this just loads the default site settings
my $tt = Template->new(
    { INCLUDE_PATH => [ $localtemplatepath, $htmltemplatepath ] } );

# print the stock header (banner and title)
$tt->process(
    'inventory_header.tt',
    {
        pagetitle    => $PAGETITLE,
        sectiontitle => $SECTIONTITLE,
        pagename     => $PAGENAME,
        baseurl      => $BASEURL,
        groupname    => $CONFIG->{'general'}->{groupname},
        appname      => $CONFIG->{'general'}->{appname},
        companyname  => $CONFIG->{'general'}->{companyname},
    }
);

# invalid users get no further than the next line
if ( !exists $CONFIG->{general}->{demo} ) {
    Inventory::acl_checker( $tt, $valid_users );
}

# I differentiate between get and post, not everyone agrees with this but
# there is an RFC2616 that suggests that we should. I do so for behaviour not
# security reasons.
my %gets = Inventory::populate_query_fields();    # not strictly a get field
my %POSTS;
if ( $ENV{REQUEST_METHOD} eq 'POST' ) {
    %POSTS = $q->Vars;
}

my $edit;
my $delete;
if ( $gets{'contract_id'} && $gets{'contract_id'} =~ m/^[\d]+$/x ) {
    if ( exists $gets{'action'} and $gets{'action'} eq 'edit' ) {
        $logger->info(
"$ENV{REMOTE_USER} takes first step to edit a contract ID $gets{'contract_id'}"
        );
        $edit = $gets{'contract_id'};
    }
    elsif ( exists $gets{'action'} and $gets{'action'} eq 'delete' ) {
        $logger->info(
"$ENV{REMOTE_USER} takes first step to delete a contract ID $gets{'contract_id'}"
        );
        $delete = $gets{'contract_id'};
    }
}

my %message;    # communicate success or failure

# are we creating/editing or deleting?
if ( $POSTS{'contract_id'} ) {
    if ( exists $POSTS{'action'} and $POSTS{'action'} eq 'delete' ) {
        $logger->info(
"$ENV{REMOTE_USER} posts to delete a contract ID $POSTS{'contract_id'}"
        );
        %message = %{
            Inventory::Contracts::delete_contracts( $dbh,
                $POSTS{'contract_id'} )
          };
    }
    elsif ( exists $POSTS{'action'} and $POSTS{'action'} eq 'edit' ) {
        $logger->info("$ENV{REMOTE_USER} posts to edit $POSTS{'contract_id'}");
        %message = %{ Inventory::Contracts::edit_contracts( $dbh, \%POSTS ) };
    }
}
elsif ( exists $POSTS{'action'} and $POSTS{'action'} eq 'create' ) {
    $logger->info("$ENV{REMOTE_USER} posts to create a new contract ID");
    %message = %{ Inventory::Contracts::create_contracts( $dbh, \%POSTS ) };
}

# list all the contracts (array of hashes)
my @contracts     = Inventory::Contracts::get_contracts_info($dbh);
my @invoices      = Inventory::Invoices::get_invoices_info($dbh);
my @servicelevels = Inventory::Servicelevels::get_servicelevels_info($dbh);

$tt->process(
    'inventory_contracts.tt',
    {
        contracts     => \@contracts,
        invoices      => \@invoices,
        servicelevels => \@servicelevels,
        message       => \%message,
        edit          => $edit,
        delete        => $delete,
    }
) || Carp::croak "Error: can't load the page due to " . $tt->error() . "\n";
$tt->process( 'inventory_footer.tt', {} );

__END__

=head1 NAME
Contracts

=head1 VERSION

This documentation refers to version 1.0.0 

=head1 USAGE

=head1 REQUIRED ARGUMENTS

=head1 OPTIONS

=head1 DESCRIPTION

Lets the user view and edit the list of hardware contracts that the inventory database knows about

=head1 DIAGNOSTICS

error messages

=head1 CONFIGURATION

/srv/www/$HOSTNAME/lib/inventory.ini

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

none known

=head1 BUGS AND LIMITATIONS

Report any found to <guyjohnedwards@gmail.com>

=head1 AUTHOR

Guy Edwards, maintained by <guyjohnedwards@gmail.com>

=head1 LICENSE AND COPYRIGHT

Network Device Inventory - keep a database of devices to feed other systems
(such as monitoring software). Copyright 2007 University of Oxford

This program is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free Software
Foundation; either version 2 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.  See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program; if not, write to the Free Software Foundation, Inc., 51 Franklin
Street, Fifth Floor, Boston, MA 02110-1301 USA.

The University of Oxford agrees to the release under the GPL of in the program
`Inventory' written by Guy Edwards as agreed by Dr. Stuart Lee, Director of
Oxford University Computer Services.

Oliver Gorwits, who in 2008 contributed sections of the hostgroups programming
also agreed to the code release under the GPL.
