#!/usr/bin/perl

use warnings FATAL => 'all';
use strict;
our $VERSION = qw('1.0.0');

use CGI;
use Template;
use Config::Tiny;
use DBI;
use DBD::Pg;

use Inventory;
use Inventory::Hosts;
use Inventory::Hostgroups;
use Inventory::Memberships;

my $BASEURL = CGI::url( -base => 1 );
my $HOSTNAME = $BASEURL;

# remove everything from the first . onwards
$HOSTNAME =~ s/\..*$//gx;

# remove everything from the start to the last /
$HOSTNAME =~ s/^.*\///gx;

my $CONFIG      = Config::Tiny->new();
my $CONFIG_FILE = "/srv/www/$HOSTNAME/lib/inventory.ini";
$CONFIG = Config::Tiny->read("$CONFIG_FILE");

my $dbhost      = $CONFIG->{'database'}->{db_host};
my $dbname      = $CONFIG->{'database'}->{db_name};
my $dbuser      = $CONFIG->{'database'}->{db_user};
my $dbpass      = $CONFIG->{'database'}->{db_pass};
my $dbh         = Inventory::dbconnect( $dbname, $dbuser, $dbhost, $dbpass );
my $valid_users = $CONFIG->{'general'}->{allowed_users};
my $htmltemplatepath  = $CONFIG->{'locations'}->{tt_dir};
my $localtemplatepath = $CONFIG->{'locations'}->{local_tt_dir};
my $logconfigfile     = $CONFIG->{'locations'}->{log_conf};

my @message_store;    # communicate success or failure, sink or swim

#############################################################################
#                                     main                                  #
#############################################################################

my $q = CGI->new();
print $q->header();

# this just loads the default site settings
my $tt = Template->new(
    { INCLUDE_PATH => [ $localtemplatepath, $htmltemplatepath ] } );

# print the stock header (banner and title)
$tt->process(
    'inventory_header.tt',
    {
        pagetitle   => 'Group Memberships',
        baseurl     => $BASEURL,
        groupname   => $CONFIG->{'general'}->{groupname},
        appname     => $CONFIG->{'general'}->{appname},
        companyname => $CONFIG->{'general'}->{companyname},
    }
);

# invalid users get no further than the next line
if ( !exists $CONFIG->{general}->{demo} ) {
    Inventory::acl_checker( $tt, $valid_users );
}

# I differenciate between get and post, not everyone agrees with this but
# there is an RFC2616 that suggests that we should. I do so for behaviour not
# security reasons.
#
my %gets = Inventory::populate_query_fields();    # not strictly a get field
my %POSTS;
if ( $ENV{REQUEST_METHOD} eq 'POST' ) {
    %POSTS = $q->Vars;
}

my $edit;
if ( $gets{'membership_id'} && $gets{'membership_id'} !~ m/\D/x ) {
    $edit = $gets{'membership_id'};
}

if ( $POSTS{'membership_id'} ) {
    if ( $POSTS{'submit'} eq 'Delete Entry' ) {
        $logger->info(
            "$ENV{REMOTE_USER} posts to delete ID $POSTS{'membership_id'}");
        @message_store =
          Inventory::Memberships::delete_memberships( $dbh, \%POSTS );
    }
    else {
        $logger->info(
            "$ENV{REMOTE_USER} posts to edit ID $POSTS{'membership_id'}");
        @message_store =
          Inventory::Memberships::edit_memberships( $dbh, \%POSTS );
    }
}
elsif ( $POSTS{'hostgroup_id'} ) {
    $logger->info(
"$ENV{REMOTE_USER} posts to create a new membership, hostgroup_id $POSTS{'hostgroup_id'}"
    );
    @message_store =
      Inventory::Memberships::create_memberships( $dbh, \%POSTS );
}

# list all the memberships (array of hashes)
my @memberships = Inventory::Memberships::get_memberships_info($dbh);

# list all the hosts (array of hashes)
my @hosts = Inventory::Hosts::get_hosts_info($dbh);

# list all the hostgroups (array of hashes)
my @hostgroups = Inventory::Hostgroups::get_hostgroups_info($dbh);

# get a count per hostgroup
my %group_summary =
  %{ Inventory::Memberships::count_memberships( $dbh, 'group' ) };
my %host_summary =
  %{ Inventory::Memberships::count_memberships( $dbh, 'host' ) };

$tt->process(
    'inventory_memberships.tt',
    {
        memberships   => \@memberships,
        message_store => \@message_store,
        edit          => $edit,
        hostgroups    => \@hostgroups,
        hosts         => \@hosts,
        group_summary => \%group_summary,
        host_summary  => \%host_summary,
    }
);
$tt->process( 'inventory_footer.tt', {} );

__END__

=head1 NAME

=head1 VERSION

This documentation refers to version 1.0.0 

=head1 USAGE

=head1 REQUIRED ARGUMENTS

=head1 OPTIONS

=head1 DESCRIPTION

=head1 DIAGNOSTICS

error messages

=head1 CONFIGURATION AND ENVIRONMENT

/srv/www/$HOSTNAME/lib/inventory.ini

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

none known

=head1 BUGS AND LIMITATIONS

Report any found to <guyjohnedwards@gmail.com>

=head1 AUTHOR

Guy Edwards, maintained by <guyjohnedwards@gmail.com>

=head1 LICENSE AND COPYRIGHT

Network Device Inventory - keep a database of devices to feed other systems
(such as monitoring software). Copyright 2007 University of Oxford

This program is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free Software
Foundation; either version 2 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.  See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program; if not, write to the Free Software Foundation, Inc., 51 Franklin
Street, Fifth Floor, Boston, MA 02110-1301 USA.

The University of Oxford agrees to the release under the GPL of in the program
`Inventory' written by Guy Edwards as agreed by Dr. Stuart Lee, Director of
Oxford University Computer Services.

Oliver Gorwits, who in 2008 contributed sections of the hostgroups programming
also agreed to the code release under the GPL.
