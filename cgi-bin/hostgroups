#!/usr/bin/perl -T
#
# Name: hostgroups
# Creator: Guy Edwards
# Created: 2008-07-28
# Description: Hostgroups administration page for the inventory application
#

use warnings FATAL => 'all';
use strict;

use CGI;
use Config::Tiny;
use DBI;
use DBD::Pg;
use Template;

use Inventory qw(dbconnect populate_query_fields acl_checker is_superuser);
use Inventory::Hostgroups
  qw(create_hostgroups edit_hostgroups get_hostgroups_info);
use Inventory::Memberships qw(count_memberships);

my $BASEURL = CGI::url( -base => 1 );
my $HOSTNAME = $BASEURL;

# remove everything from the first . onwards
$HOSTNAME =~ s/\..*$//gx;

# remove everything from the start to the last /
$HOSTNAME =~ s/^.*\///gx;

my $CONFIG      = Config::Tiny->new();
my $CONFIG_FILE = "/srv/www/$HOSTNAME/lib/inventory.ini";
$CONFIG = Config::Tiny->read("$CONFIG_FILE");

my $dbhost           = $CONFIG->{'database'}->{db_host};
my $dbname           = $CONFIG->{'database'}->{db_name};
my $dbuser           = $CONFIG->{'database'}->{db_user};
my $dbpass           = $CONFIG->{'database'}->{db_pass};
my $dbh              = dbconnect( $dbname, $dbuser, $dbhost, $dbpass );
my $valid_users      = $CONFIG->{'general'}->{allowed_users};
my $htmltemplatepath = $CONFIG->{'locations'}->{tt_dir};

#############################################################################
#                                     main                                  #
#############################################################################

my $q = CGI->new();
print $q->header();

# this just loads the default site settings
my $tt = Template->new( { INCLUDE_PATH => $htmltemplatepath, } );

# print the stock header (banner and title)
$tt->process(
    'inventory_header.tt',
    {
        pagetitle  => 'Hostgroups',
        baseurl    => $BASEURL,
        groupname  => $CONFIG->{'general'}->{groupname},
        appname    => $CONFIG->{'general'}->{appname},
        companyname=> $CONFIG->{'general'}->{companyname},
    }
);

# invalid users get no further than the next line
if(! exists $CONFIG->{general}->{demo} ){ Inventory::acl_checker( $tt, $valid_users ); }

# I differenciate between get and post, not everyone agrees with this but
# there is an RFC2616 that suggests that we should. I do so for behaviour not
# security reasons.
my %gets = populate_query_fields();    # not strictly a get field of course
my %POSTS;
if ( $ENV{REQUEST_METHOD} eq 'POST' ) {
    %POSTS = $q->Vars;
}

my $edit;
if ( $gets{'hostgroup_id'} && $gets{'hostgroup_id'} !~ m/\D/x ) {
    $edit = $gets{'hostgroup_id'};
}

my %message;    # communicate success or failure, sink or swim

# are we editing an existing entry?
if ( $POSTS{'hostgroup_id'} ) {
    %message = %{ Inventory::Hostgroups::edit_hostgroups( $dbh, \%POSTS ) };
}
elsif ( $POSTS{'hostgroup_name'} ) {

    # are we creating a new entry?
    %message = %{ Inventory::Hostgroups::create_hostgroups( $dbh, \%POSTS ) };
}

# list all the hostgroups (array of hashes)
my @hostgroups =
  Inventory::Hostgroups::get_hostgroups_info( $dbh, '', $gets{'orderby'},
    $gets{'direction'} );

# get a count per hostgroup
my %group_summary =
  %{ Inventory::Memberships::count_memberships( $dbh, 'group' ) };
my %host_summary =
  %{ Inventory::Memberships::count_memberships( $dbh, 'host' ) };

$tt->process(
    'inventory_hostgroups.tt',
    {
        hostgroups    => \@hostgroups,
        message       => \%message,
        edit          => $edit,
        host_summary  => \%host_summary,
        group_summary => \%group_summary,
    }
);
$tt->process('inventory_footer.tt');

__END__

=head1 NAME
hostgroups

=head1 VERSION

This documentation refers to  version 

=head1 USAGE

=head1 REQUIRED ARGUMENTS

=head1 OPTIONS

=head1 DESCRIPTION

=head1 DIAGNOSTICS

error messages

=head1 CONFIGURATION

Configuration files used?

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

none known

=head1 BUGS AND LIMITATIONS

Report any found to <guyjohnedwards@gmail.com>

=head1 AUTHOR

Guy Edwards, maintained by <guyjohnedwards@gmail.com>

=head1 LICENSE AND COPYRIGHT

Network Device Inventory - keep a database of devices to feed other systems
(such as monitoring software). Copyright 2007 University of Oxford

This program is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free Software
Foundation; either version 2 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.  See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program; if not, write to the Free Software Foundation, Inc., 51 Franklin
Street, Fifth Floor, Boston, MA 02110-1301 USA.

The University of Oxford disclaims all copyright interest in the program
`Inventory' written by Guy Edwards as agreed by Dr. Stuart Lee, Director of
Oxford University Computer Services.

Oliver Gorwits, who in 2008 contributed sections of the hostgroups programming
also disclaims all copyright interest in the program.
