#!/usr/bin/perl

use warnings FATAL => 'all';
use strict;
our $VERSION = qw('1.0.0');

use CGI;
use Config::Tiny;
use DBI;
use DBD::Pg;
use Template;

use Inventory;
use Inventory::Hosts;
use Inventory::Interfaces;

my $BASEURL = CGI::url( -base => 1 );
my $HOSTNAME = $BASEURL;

# remove everything from the first . onwards
$HOSTNAME =~ s/\..*$//gx;

# remove everything from the start to the last /
$HOSTNAME =~ s/^.*\///gx;

my $CONFIG      = Config::Tiny->new();
my $CONFIG_FILE = "/srv/www/$HOSTNAME/lib/inventory.ini";
$CONFIG = Config::Tiny->read("$CONFIG_FILE");

my $dbhost           = $CONFIG->{'database'}->{db_host};
my $dbname           = $CONFIG->{'database'}->{db_name};
my $dbuser           = $CONFIG->{'database'}->{db_user};
my $dbpass           = $CONFIG->{'database'}->{db_pass};
my $dbh              = Inventory::dbconnect( $dbname, $dbuser, $dbhost, $dbpass );
my $valid_users      = $CONFIG->{'general'}->{allowed_users};
my $htmltemplatepath = $CONFIG->{'locations'}->{tt_dir};
my $localtemplatepath = $CONFIG->{'locations'}->{local_tt_dir};
my $logconfigfile    = $CONFIG->{'locations'}->{log_conf};

#############################################################################
#                                     main                                  #
#############################################################################

my $q = CGI->new();
print $q->header();

# this just loads the default site settings
my $tt = Template->new( { INCLUDE_PATH => [$localtemplatepath, $htmltemplatepath] } );

# print the stock header (banner and title)
$tt->process(
    'inventory_header.tt',
    {
        pagetitle  => 'Interface Problems',
        baseurl    => $BASEURL,
        groupname  => $CONFIG->{'general'}->{groupname},
        appname    => $CONFIG->{'general'}->{appname},
        companyname=> $CONFIG->{'general'}->{companyname},
    }
);

# invalid users get no further than the next line
if(! exists $CONFIG->{general}->{demo} ){ Inventory::acl_checker( $tt, $valid_users ); }

# I differenciate between get and post, not everyone agrees with this but
# there is an RFC2616 that suggests that we should. I do so for behaviour not
# security reasons.
my %gets = Inventory::populate_query_fields();    # not strictly a get field
my %POSTS;
if ( $ENV{REQUEST_METHOD} eq 'POST' ) {
    %POSTS = $q->Vars;
}

# list all the interfaces (array of hashes)
my @interfaces = Inventory::Interfaces::get_interfaces_info($dbh);

# list all the hosts (array of hashes)
my @hosts = Inventory::Hosts::get_hosts_info($dbh);

my @bugarray = interfaces_bugcheck( \@interfaces, \@hosts );

# list all hosts with no interface
my @hosts_no_interface = @{ $bugarray[0] };

# list all hosts with one interface and no primary interface
my @hosts_singleno_primary = @{ $bugarray[1] };

# list all hosts with multiple interfaces and no primary interface
my @hosts_multino_primary = @{ $bugarray[2] };

my %host_to_interfaces =
  %{ hash_hostid_to_tointerfaces( \@interfaces, \@hosts ) };

$tt->process(
    'inventory_problems_interfaces.tt',
    {
        interfaces             => \@interfaces,
        hosts                  => \@hosts,
        hosts_no_interface     => \@hosts_no_interface,
        hosts_singleno_primary => \@hosts_singleno_primary,
        hosts_multino_primary  => \@hosts_multino_primary,
        host_to_interfaces     => \%host_to_interfaces,
    }
);
$tt->process( 'inventory_footer.tt', {} );

###########################################################################
#                             subroutines                                 #
###########################################################################

sub interfaces_bugcheck {

    # find hosts with only one interface
    # AND that interface isn't marked as primary
    #
    my $ref1        = shift;
    my $ref2        = shift;
    my @binterfaces = @{$ref1};
    my @bhosts      = @{$ref2};

    my @bhosts_no_interface;
    my @bhosts_singleno_primary;
    my @bhosts_multino_primary;

    foreach my $hostref (@bhosts) {
        my %host_details    = %{$hostref};
        my $interface_total = 0;
        my $has_primary     = 0;
        next if $host_details{'status_state'} ne 'ACTIVE';

        foreach my $interfaceref (@binterfaces) {
            my %interface_details = %{$interfaceref};
            if ( $host_details{'id'} == $interface_details{'host_id'} ) {

                # host has an interface
                $interface_total++;

                # if it's got more than one interface, ignore it
                if ( $interface_details{'isprimary'} == 1 ) {
                    $has_primary = 1;
                }
            }
        }

        if ( $interface_total == 0 ) {

            # no interfaces
            push @bhosts_no_interface, \%host_details;
        }

        if ( $interface_total == 1 && $has_primary == 0 ) {

            # one interface, no primary
            push @bhosts_singleno_primary, \%host_details;
        }

        if ( $interface_total > 1 && $has_primary == 0 ) {

            # multiple interfaces, no primary
            push @bhosts_multino_primary, \%host_details;
        }

    }

    return \@bhosts_no_interface, \@bhosts_singleno_primary,
      \@bhosts_multino_primary;
}

sub hash_hostid_to_tointerfaces {
    my $ref1        = shift;
    my $ref2        = shift;
    my @ninterfaces = @{$ref1};
    my @nhosts      = @{$ref2};

    my %return_hash;

    foreach my $hostref (@nhosts) {
        my %host_details = %{$hostref};
        my @host_interfaces;

        foreach my $interfaceref (@ninterfaces) {
            my %interface_details = %{$interfaceref};

            if ( $host_details{'id'} == $interface_details{'host_id'} ) {

                # host has an interface
                push @host_interfaces, $interface_details{'id'};
            }
        }

        my $host_id = $host_details{'id'};
        $return_hash{$host_id} = \@host_interfaces;
    }

    return \%return_hash;
}

__END__

=head1 NAME

=head1 VERSION

This documentation refers to production version 1.0.0

=head1 USAGE

=head1 REQUIRED ARGUMENTS

=head1 OPTIONS

=head1 DESCRIPTION

=head1 DIAGNOSTICS

error messages

=head1 CONFIGURATION

/srv/www/$HOSTNAME/lib/inventory.ini

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

none known

=head1 BUGS AND LIMITATIONS

Report any found to <guyjohnedwards@gmail.com>

=head1 AUTHOR

Guy Edwards, maintained by <guyjohnedwards@gmail.com>

=head1 LICENSE AND COPYRIGHT

Network Device Inventory - keep a database of devices to feed other systems
(such as monitoring software). Copyright 2007 University of Oxford

This program is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free Software
Foundation; either version 2 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.  See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program; if not, write to the Free Software Foundation, Inc., 51 Franklin
Street, Fifth Floor, Boston, MA 02110-1301 USA.

The University of Oxford disclaims all copyright interest in the program
`Inventory' written by Guy Edwards as agreed by Dr. Stuart Lee, Director of
Oxford University Computer Services.

Oliver Gorwits, who in 2008 contributed sections of the hostgroups programming
also disclaims all copyright interest in the program.
