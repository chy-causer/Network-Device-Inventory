#!/usr/bin/perl 

use warnings FATAL => 'all';
use strict;
our $VERSION = '1.00';

use Carp;
use CGI;
use Config::Tiny;
use DateTime;
use DBD::Pg;
use DBI;
use Log::Log4perl;
use Readonly;
use Template;

use Inventory 1.0;
use Inventory::Hosts 1.0;
use Inventory::Locations 1.0;
use Inventory::Invoices 1.0;
use Inventory::Models 1.0;
use Inventory::Status 1.0;
use Inventory::Ups 1.0;

my $BASEURL = CGI::url( -base => 1 );
my $HOSTNAME = $BASEURL;

# remove everything from the first . onwards
$HOSTNAME =~ s/\..*$//gx;

# remove everything from the start to the last /
$HOSTNAME =~ s/^.*\///gx;

my $CONFIG      = Config::Tiny->new();
my $CONFIG_FILE = "/srv/www/$HOSTNAME/lib/inventory.ini";
$CONFIG = Config::Tiny->read("$CONFIG_FILE");

my $dbhost      = $CONFIG->{'database'}->{db_host};
my $dbname      = $CONFIG->{'database'}->{db_name};
my $dbuser      = $CONFIG->{'database'}->{db_user};
my $dbpass      = $CONFIG->{'database'}->{db_pass};
my $dbh         = Inventory::dbconnect( $dbname, $dbuser, $dbhost, $dbpass );
my $valid_users = $CONFIG->{'general'}->{allowed_users};
my $htmltemplatepath  = $CONFIG->{'locations'}->{tt_dir};
my $localtemplatepath = $CONFIG->{'locations'}->{local_tt_dir};
my $logconfigfile     = $CONFIG->{'locations'}->{log_conf};

Log::Log4perl->init($logconfigfile);
my $logger = Log::Log4perl->get_logger();

Readonly my $PAGETITLE => 'Hosts';
Readonly my $SECTIONTITLE => 'admin';

#############################################################################
#                                     main                                  #
#############################################################################

my $q = CGI->new();
print $q->header();

# this just loads the default site settings
my $tt = Template->new(
    { INCLUDE_PATH => [ $localtemplatepath, $htmltemplatepath ] } );

# print the stock header (banner and title)
$tt->process(
    'inventory_header.tt',
    {
        pagetitle    => $PAGETITLE,
        sectiontitle => $SECTIONTITLE,
        baseurl      => $BASEURL,
        groupname    => $CONFIG->{'general'}->{groupname},
        appname      => $CONFIG->{'general'}->{appname},
        companyname  => $CONFIG->{'general'}->{companyname},
    }
);

# invalid users get no further than the next line
if ( !exists $CONFIG->{general}->{demo} ) {
    Inventory::acl_checker( $tt, $valid_users );
}

# I differentiate between get and post, not everyone agrees with this but
# there is an RFC2616 that suggests that we should. I do so for behaviour not
# security reasons.
my %gets = Inventory::populate_query_fields();    # not strictly a get field
my %POSTS;
if ( $ENV{REQUEST_METHOD} eq 'POST' ) {
    %POSTS = $q->Vars;
}

my ( $edit, $delete );
if ( $gets{'host_id'} && $gets{'host_id'} =~ m/^[\d]+$/x ) {
    if ( exists $gets{'action'} and $gets{'action'} eq 'delete' ) {
        $logger->debug(
            "$ENV{REMOTE_USER} takes first step to delete ID $gets{'host_id'}");
        $delete = $gets{'host_id'};
    }
    elsif ( exists $gets{'action'} and $gets{'action'} eq 'edit' ) {
        $logger->debug(
            "$ENV{REMOTE_USER} takes first step to edit ID $gets{'host_id'}");
        $edit = $gets{'host_id'};
    }
    else {
        $edit = $gets{'host_id'};
    }
}

my %message;    # communicate success or failure, sink or swim

# are we editing/deleting an existing entry?
if ( exists $POSTS{'action'} ) {
    
    # so that we can share javascript functions between this page and the
    # edithosts main page
    if( exists $POSTS{'arec_x'} ) {
        $POSTS{'host_name'} = $POSTS{'arec_x'};
    }

    if ( $POSTS{'action'} eq 'delete' ) {
        $logger->info("$ENV{REMOTE_USER} posts to delete ID $POSTS{'host_id'}");
        %message =
          %{ Inventory::Hosts::delete_hosts( $dbh, $POSTS{'host_id'} ) };
    }
    elsif ( $POSTS{'action'} eq 'edit' ) {
        $logger->info("$ENV{REMOTE_USER} posts to edit ID $POSTS{'host_id'}");
        %message = %{ Inventory::Hosts::edit_hosts( $dbh, \%POSTS ) };
    }
    elsif ( $POSTS{'action'} eq 'create' ) {
        $logger->info("$ENV{REMOTE_USER} posts to create a new host");
        %message = %{ Inventory::Hosts::create_hosts( $dbh, \%POSTS ) };
    }
}

# list all the hosts (array of hashes)
my @hosts = Inventory::Hosts::get_hosts_info($dbh);

# list all the locations (array of hashes)
my @locations = Inventory::Locations::get_locations_info($dbh);
my @models    = Inventory::Models::get_models_info($dbh);
my @status    = Inventory::Status::get_status_info($dbh);
my @ups       = Inventory::Ups::get_ups_info($dbh);
my @invoices  = Inventory::Invoices::get_invoices_info($dbh);

my @dt = split m/T/x, DateTime->now();

my $today = $dt[0];
my @parts = split m/-/x, $today;
$parts[0]--;
my $lastyear = join '-', @parts;

$tt->process(
    'inventory_hosts.tt',
    {
        hosts     => \@hosts,
        locations => \@locations,
        models    => \@models,
        status    => \@status,
        message   => \%message,
        edit      => $edit,
        delete    => $delete,
        today     => $today,
        lastyear  => $lastyear,
        ups       => \@ups,
        invoices  => \@invoices,
        host_id   => $gets{'host_id'},
    }
) || Carp::croak "Error: can't load the page due to " . $tt->error() . "\n";

$tt->process('inventory_footer.tt');

__END__

=head1 NAME
hosts

=head1 VERSION

This documentation refers to version 1.0.0 

=head1 USAGE

=head1 REQUIRED ARGUMENTS

=head1 OPTIONS

=head1 DESCRIPTION

=head1 DIAGNOSTICS

error messages

=head1 CONFIGURATION

/srv/www/$HOSTNAME/lib/inventory.ini

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

none known

=head1 BUGS AND LIMITATIONS

Report any found to <guyjohnedwards@gmail.com>

=head1 AUTHOR

Guy Edwards, maintained by <guyjohnedwards@gmail.com>

=head1 LICENSE AND COPYRIGHT

Network Device Inventory - keep a database of devices to feed other systems
(such as monitoring software). Copyright 2007 University of Oxford

This program is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free Software
Foundation; either version 2 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.  See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program; if not, write to the Free Software Foundation, Inc., 51 Franklin
Street, Fifth Floor, Boston, MA 02110-1301 USA.

The University of Oxford agrees to the release under the GPL of in the program
`Inventory' written by Guy Edwards as agreed by Dr. Stuart Lee, Director of
Oxford University Computer Services.

Oliver Gorwits, who in 2008 contributed sections of the hostgroups programming
also agreed to the code release under the GPL.
