#!/usr/bin/perl -T 
#
# Name: hosts
# Creator: Guy Edwards
# Created: 2008-07-28
# Description: Hosts administration page for the inventory application
#
# $Id: hosts 3539 2012-02-10 16:01:31Z guy $
# $LastChangedBy: guy $
# $LastChangedDate: 2012-02-10 16:01:31 +0000 (Fri, 10 Feb 2012) $
# $LastChangedRevision: 3539 $
# $uid: pmrQuzOM61NC31L8ohHDNOLpzNPilUf4nGZ6DjRl8l14l $
#

use warnings FATAL => 'all';
use strict;

use CGI;
use Config::Tiny;
use DateTime;
use DBD::Pg;
use DBI;
use Template;

use Inventory qw(dbconnect populate_query_fields acl_checker);
use Inventory::Hosts qw(create_hosts edit_hosts get_hosts_info);
use Inventory::Locations qw(get_locations_info);
use Inventory::Invoices;
use Inventory::Models qw(get_models_info);
use Inventory::Status qw(get_status_info);
use Inventory::Ups qw(get_ups_info);

my $BASEURL = CGI::url( -base => 1 );
my $HOSTNAME = $BASEURL;

# remove everything from the first . onwards
$HOSTNAME =~ s/\..*$//gx;

# remove everything from the start to the last /
$HOSTNAME =~ s/^.*\///gx;

my $CONFIG      = Config::Tiny->new();
my $CONFIG_FILE = "/srv/www/$HOSTNAME/lib/inventory.ini";
$CONFIG = Config::Tiny->read("$CONFIG_FILE");

my $dbhost           = $CONFIG->{'database'}->{db_host};
my $dbname           = $CONFIG->{'database'}->{db_name};
my $dbuser           = $CONFIG->{'database'}->{db_user};
my $dbpass           = $CONFIG->{'database'}->{db_pass};
my $dbh              = dbconnect( $dbname, $dbuser, $dbhost, $dbpass );
my $valid_users      = $CONFIG->{'general'}->{allowed_users};
my $htmltemplatepath = $CONFIG->{'locations'}->{tt_dir};

#############################################################################
#                                     main                                  #
#############################################################################

my $q = CGI->new();
print $q->header();

# this just loads the default site settings
my $tt = Template->new( { INCLUDE_PATH => $htmltemplatepath, } );

# print the stock header (banner and title)
$tt->process(
    'inventory_header.tt',
    {
        pagetitle  => 'Hosts',
        baseurl    => $BASEURL,
        groupname  => $CONFIG->{'general'}->{groupname},
        appname    => $CONFIG->{'general'}->{appname},
        companyname=> $CONFIG->{'general'}->{companyname},
    }
);

# invalid users get no further than the next line
if(! exists $CONFIG->{general}->{demo} ){ Inventory::acl_checker( $tt, $valid_users ); }

# I differenciate between get and post, not everyone agrees with this but
# there is an RFC2616 that suggests that we should. I do so for behaviour not
# security reasons.
my %gets =
  Inventory::populate_query_fields();    # not strictly a get field of course
my %POSTS;
if ( $ENV{REQUEST_METHOD} eq 'POST' ) {
    %POSTS = $q->Vars;
}

my ( $edit, $delete );
if ( $gets{'host_id'} && $gets{'host_id'} =~ m/^[\d]+$/x ) {
    if ( exists $gets{'action'} and $gets{'action'} eq 'delete' ) {
        $delete = $gets{'host_id'};
    }
    elsif ( exists $gets{'action'} and $gets{'action'} eq 'edit' ) {
        $edit = $gets{'host_id'};
    }
    else {
        $edit = $gets{'host_id'};
    }
}

my %message;    # communicate success or failure, sink or swim

# are we editing/deleting an existing entry?
if ( exists $POSTS{'action'} ) {
    if ( $POSTS{'action'} eq 'delete' ) {
        %message =
          %{ Inventory::Hosts::delete_hosts( $dbh, $POSTS{'host_id'} ) };
    }
    elsif ( $POSTS{'action'} eq 'edit' ) {
        %message = %{ Inventory::Hosts::edit_hosts( $dbh, \%POSTS ) };
    }
    elsif ( $POSTS{'action'} eq 'create' ) {
        %message = %{ Inventory::Hosts::create_hosts( $dbh, \%POSTS ) };
    }
}

# list all the hosts (array of hashes)
my @hosts = Inventory::Hosts::get_hosts_info($dbh);

# list all the locations (array of hashes)
my @locations = Inventory::Locations::get_locations_info($dbh);
my @models    = Inventory::Models::get_models_info($dbh);
my @status    = Inventory::Status::get_status_info($dbh);
my @ups       = Inventory::Ups::get_ups_info($dbh);
my @invoices  = Inventory::Invoices::get_invoices_info($dbh);

my @dt = split m/T/x, DateTime->now();

my $today = $dt[0];
my @parts = split m/-/x, $today;
$parts[0]--;
my $lastyear = join '-', @parts;

$tt->process(
    'inventory_hosts.tt',
    {
        hosts     => \@hosts,
        locations => \@locations,
        models    => \@models,
        status    => \@status,
        message   => \%message,
        edit      => $edit,
        delete    => $delete,
        today     => $today,
        lastyear  => $lastyear,
        ups       => \@ups,
        invoices  => \@invoices,
    }
);
$tt->process('inventory_footer.tt');

__END__

=head1 NAME
hosts

=head1 VERSION

This documentation refers to  version 

=head1 USAGE

=head1 REQUIRED ARGUMENTS

=head1 OPTIONS

=head1 DESCRIPTION

=head1 DIAGNOSTICS

error messages

=head1 CONFIGURATION

Configuration files used?

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

none known

=head1 BUGS AND LIMITATIONS

Report any found to <guyjohnedwards@gmail.com>

=head1 AUTHOR

Guy Edwards, maintained by <guyjohnedwards@gmail.com>

=head1 LICENSE AND COPYRIGHT

Network Device Inventory - keep a database of devices to feed other systems
(such as monitoring software). Copyright 2007 University of Oxford

This program is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free Software
Foundation; either version 2 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.  See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program; if not, write to the Free Software Foundation, Inc., 51 Franklin
Street, Fifth Floor, Boston, MA 02110-1301 USA.

The University of Oxford disclaims all copyright interest in the program
`Inventory' written by Guy Edwards as agreed by Dr. Stuart Lee, Director of
Oxford University Computer Services.

Oliver Gorwits, who in 2008 contributed sections of the hostgroups programming
also disclaims all copyright interest in the program.
