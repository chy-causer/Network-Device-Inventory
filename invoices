#!/usr/bin/perl -T
#
# Name: invoices
# Creator: Guy Edwards
# Created: 2012-01-30
# Description: Invoices administration page for the inventory application
#
# $Id: invoices 3524 2012-02-06 15:52:19Z guy $
# $LastChangedBy: guy $
# $LastChangedDate: 2012-02-06 15:52:19 +0000 (Mon, 06 Feb 2012) $
# $LastChangedRevision: 3524 $
#

use warnings FATAL => 'all';
use strict;

use CGI;
use Config::Tiny;
use DBI;
use DBD::Pg;
use Template;

use Inventory qw(dbconnect populate_query_fields acl_checker);
use Inventory::Invoices;
use Inventory::Suppliers;
use Inventory::Contacts;

my $HOSTNAME = CGI::url( -base => 1 );
my $BASEURL = $HOSTNAME;

# remove everything from the first . onwards
$HOSTNAME =~ s/\..*$//gx;

# remove everything from the start to the last /
$HOSTNAME =~ s/^.*\///gx;

my $CONFIG      = Config::Tiny->new();
my $CONFIG_FILE = "/srv/www/$HOSTNAME/lib/inventory.ini";
$CONFIG = Config::Tiny->read("$CONFIG_FILE");

my $dbhost           = $CONFIG->{'database'}->{db_host};
my $dbname           = $CONFIG->{'database'}->{db_name};
my $dbuser           = $CONFIG->{'database'}->{db_user};
my $dbpass           = $CONFIG->{'database'}->{db_pass};
my $dbh              = dbconnect( $dbname, $dbuser, $dbhost, $dbpass );
my $valid_users      = $CONFIG->{'general'}->{allowed_users};
my $htmltemplatepath = $CONFIG->{'locations'}->{tt_dir};

#############################################################################
#                                     main                                  #
#############################################################################

my $q = CGI->new();
print $q->header();

# this just loads the default site settings
my $tt = Template->new( { INCLUDE_PATH => $htmltemplatepath, } );

# print the stock header (banner and title)
$tt->process(
    'inventory_header.tt',
    {
        pagetitle  => 'Invoices',
        baseurl    => $BASEURL,
        extramenu  => 'inventory',
        javascript => 'on',
    }
);

# invalid users get no further than the next line
Inventory::acl_checker( $tt, $valid_users );

# I differenciate between get[sic] and post, and I'm proud. I also wear suspenders.
my %gets = populate_query_fields();    # not strictly a get field of course
my %POSTS;
if ( $ENV{REQUEST_METHOD} eq 'POST' ) {
    %POSTS = $q->Vars;
}

my $edit;
my $delete;
if ( $gets{'invoice_id'} && $gets{'invoice_id'} =~ m/^[\d]+$/x ) {
    if ( exists $gets{'action'} and $gets{'action'} eq 'edit' ) {
        $edit = $gets{'invoice_id'};
    }
    elsif ( exists $gets{'action'} and $gets{'action'} eq 'delete' ) {
        $delete = $gets{'invoice_id'};
    }
}

my %message;    # communicate success or failure, sink or swim

# are we editing/deleting an existing entry?
if ( $POSTS{'invoice_id'} ) {
    if ( exists $POSTS{'action'} and $POSTS{'action'} eq 'delete' ) {
        %message =
          %{ Inventory::Invoices::delete_invoices( $dbh, $POSTS{'invoice_id'} )
          };
    }
    elsif ( exists $POSTS{'action'} and $POSTS{'action'} eq 'edit' ) {
        %message = %{ Inventory::Invoices::edit_invoices( $dbh, \%POSTS ) };
    }
}
elsif ( exists $POSTS{'action'} and $POSTS{'action'} eq 'create' ) {

    # are we creating a new entry?
    %message = %{ Inventory::Invoices::create_invoices( $dbh, \%POSTS ) };
}

my @invoices  = Inventory::Invoices::get_invoices_info($dbh);
my @contacts  = Inventory::Contacts::get_contacts_info($dbh);
my @suppliers = Inventory::Suppliers::get_suppliers_info($dbh);

$tt->process(
    'inventory_invoices.tt',
    {
        invoices  => \@invoices,
        contacts  => \@contacts,
        suppliers => \@suppliers,
        message   => \%message,
        edit      => $edit,
        delete    => $delete,
    }
);
$tt->process( 'stdfoot.tt', {} );

__END__

=head1 NAME
Invoices

=head1 VERSION

This documentation refers to  version 

=head1 USAGE

=head1 REQUIRED ARGUMENTS

=head1 OPTIONS

=head1 DESCRIPTION

Lets the user view and edit the list of hardware invoices that the inventory database knows about

=head1 DIAGNOSTICS

error messages

=head1 CONFIGURATION

Configuration files used?

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

none known

=head1 BUGS AND LIMITATIONS

Report any found to <hostmaster@ox.ac.uk>

=head1 AUTHOR

Guy Edwards, maintained by <hostmaster@ox.ac.uk>

=head1 LICENSE AND COPYRIGHT



(c) University of Oxford
