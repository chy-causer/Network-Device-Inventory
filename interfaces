#!/usr/bin/perl -T
#
# Name: interfaces
# Creator: Guy Edwards
# Created: 2008-07-28
# Description: Services administration page for the inventory application
#
# $Id: interfaces 3538 2012-02-10 15:39:37Z guy $
# $LastChangedBy: guy $
# $LastChangedDate: 2012-02-10 15:39:37 +0000 (Fri, 10 Feb 2012) $
# $LastChangedRevision: 3538 $
# $uid: gmitNg8MQ_WbPDmloXPVY0hEZU9wcxsK5ha6Z65sC52C1 $
#

use warnings FATAL => 'all';
use strict;

use CGI;
use Config::Tiny;
use DBI;
use DBD::Pg;
use Template;

use Inventory qw(dbconnect populate_query_fields acl_checker);
use Inventory::Hosts qw(get_hosts_info);
use Inventory::Interfaces
  qw(create_interfaces edit_interfaces get_interfaces_info delete_interface);

my $BASEURL = CGI::url( -base => 1 );
my $HOSTNAME = $BASEURL;

# remove everything from the first . onwards
$HOSTNAME =~ s/\..*$//gx;

# remove everything from the start to the last /
$HOSTNAME =~ s/^.*\///gx;

my $CONFIG      = Config::Tiny->new();
my $CONFIG_FILE = "/srv/www/$HOSTNAME/lib/inventory.ini";
$CONFIG = Config::Tiny->read("$CONFIG_FILE");

my $dbhost           = $CONFIG->{'database'}->{db_host};
my $dbname           = $CONFIG->{'database'}->{db_name};
my $dbuser           = $CONFIG->{'database'}->{db_user};
my $dbpass           = $CONFIG->{'database'}->{db_pass};
my $dbh              = dbconnect( $dbname, $dbuser, $dbhost, $dbpass );
my $valid_users      = $CONFIG->{'general'}->{allowed_users};
my $htmltemplatepath = $CONFIG->{'locations'}->{tt_dir};

#############################################################################
#                                     main                                  #
#############################################################################

my $q = CGI->new();
print $q->header();

# this just loads the default site settings
my $tt = Template->new( { INCLUDE_PATH => $htmltemplatepath, } );

# print the stock header (banner and title)
$tt->process(
    'inventory_header.tt',
    {
        pagetitle  => 'Interfaces',
        baseurl    => $BASEURL,
        extramenu  => 'inventory',
        javascript => 'on',
    }
);

# invalid users get no further than the next line
Inventory::acl_checker( $tt, $valid_users );

# I differenciate between get and post, not everyone agrees with this but
# there is an RFC2616 that suggests that we should. I do so for behaviour not
# security reasons.
my %gets = populate_query_fields();    # not strictly a get field of course
my %POSTS;
if ( $ENV{REQUEST_METHOD} eq 'POST' ) {
    %POSTS = $q->Vars;
}

my $edit;
my $makeprimary;
my $makehost_id;
if ( exists $gets{'interface_id'} && $gets{'interface_id'} !~ m/\D/x ) {
    $edit = $gets{'interface_id'};
}
if ( exists $gets{'isprimary'} ) {
    $makeprimary = $gets{'isprimary'};
}
if ( exists $gets{'host_id'} ) {
    $makehost_id = $gets{'host_id'};
}

my %message;    # communicate success or failure, sink or swim

# are we editing an existing entry?
if (   exists $POSTS{'interface_id'}
    && exists $POSTS{'edit'}
    && length $POSTS{'edit'} > 0 )
{
    %message = %{ Inventory::Interfaces::edit_interfaces( $dbh, \%POSTS ) };
}
elsif (exists $POSTS{'interface_id'}
    && exists $POSTS{'delete'}
    && length $POSTS{'delete'} > 0 )
{
    %message =
      %{ Inventory::Interfaces::delete_interface( $dbh, $POSTS{'interface_id'} )
      };
}
elsif ( $POSTS{'host_id'} ) {

    # are we creating a new entry?
    %message = %{ Inventory::Interfaces::create_interfaces( $dbh, \%POSTS ) };
}

# list all the interfaces (array of hashes)
my @interfaces = Inventory::Interfaces::get_interfaces_info($dbh);

# list all the hosts (array of hashes)
my @hosts = Inventory::Hosts::get_hosts_info($dbh);

$tt->process(
    'inventory_interfaces.tt',
    {
        interfaces  => \@interfaces,
        message     => \%message,
        edit        => $edit,
        hosts       => \@hosts,
        makeprimary => $makeprimary,
        makehost_id => $makehost_id,
    }
);
$tt->process('stdfoot.tt');

__END__

=head1 NAME
Interfaces

=head1 VERSION

This documentation refers to  version 

=head1 USAGE

=head1 REQUIRED ARGUMENTS

=head1 OPTIONS

=head1 DESCRIPTION

=head1 DIAGNOSTICS

error messages

=head1 CONFIGURATION

Configuration files used?

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

none known

=head1 BUGS AND LIMITATIONS

Report any found to <hostmaster@ox.ac.uk>

=head1 AUTHOR

Guy Edwards, maintained by <hostmaster@ox.ac.uk>

=head1 LICENSE AND COPYRIGHT



(c) University of Oxford


__END__

=head1 NAME

=head1 VERSION

This documentation refers to  version 

=head1 USAGE

=head1 REQUIRED ARGUMENTS

=head1 OPTIONS

=head1 DESCRIPTION

=head1 DIAGNOSTICS

error messages

=head1 CONFIGURATION

Configuration files used?

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

none known

=head1 BUGS AND LIMITATIONS

Report any found to <hostmaster@ox.ac.uk>

=head1 AUTHOR


, maintained by <hostmaster@ox.ac.uk>

=head1 LICENSE AND COPYRIGHT



(c) University of Oxford
