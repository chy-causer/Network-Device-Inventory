#!/usr/bin/perl -T
#
# Name: manufacturers
# Creator: Guy Edwards
# Created: 2008-07-28
# Description: Manufacturers administration page for the inventory
#
# $Id: manufacturers 3538 2012-02-10 15:39:37Z guy $
# $LastChangedBy: guy $
# $LastChangedDate: 2012-02-10 15:39:37 +0000 (Fri, 10 Feb 2012) $
# $LastChangedRevision: 3538 $
# $uid: D7x8JRXZaBI2yatuxgH8rWusv9no9ETLNKsVSJl6QWMfU $
#

use warnings FATAL => 'all';
use strict;

use CGI;
use Config::Tiny;
use DBI;
use DBD::Pg;
use Template;

use Inventory qw(dbconnect populate_query_fields acl_checker);
use Inventory::Manufacturers qw(create_manufacturers edit_manufacturers
  get_manufacturers_info count_models_permanufacturer count_hosts_permanufacturer);
use Inventory::Models qw(get_models_info);

my $BASEURL = CGI::url( -base => 1 );
my $HOSTNAME = $BASEURL;

# remove everything from the first . onwards
$HOSTNAME =~ s/\..*$//gx;

# remove everything from the start to the last /
$HOSTNAME =~ s/^.*\///gx;

my $CONFIG      = Config::Tiny->new();
my $CONFIG_FILE = "/srv/www/$HOSTNAME/lib/inventory.ini";
$CONFIG = Config::Tiny->read("$CONFIG_FILE");

my $dbhost      = $CONFIG->{'database'}->{db_host};
my $dbname      = $CONFIG->{'database'}->{db_name};
my $dbuser      = $CONFIG->{'database'}->{db_user};
my $dbpass      = $CONFIG->{'database'}->{db_pass};
my $dbh         = Inventory::dbconnect( $dbname, $dbuser, $dbhost, $dbpass );
my $valid_users = $CONFIG->{'general'}->{allowed_users};
my $htmltemplatepath = $CONFIG->{'locations'}->{tt_dir};

#############################################################################
#                                     main                                  #
#############################################################################

my $q = CGI->new();
print $q->header();

# this just loads the default site settings
my $tt = Template->new( { INCLUDE_PATH => $htmltemplatepath, } );

# print the stock header (banner and title)
$tt->process(
    'inventory_header.tt',
    {
        pagetitle  => 'Manufacturers',
        baseurl    => $BASEURL,
        extramenu  => 'inventory',
        javascript => 'on',
    }
);

# invalid users get no further than the next line
Inventory::acl_checker( $tt, $valid_users );

# I differenciate between get and post, not everyone agrees with this but
# there is an RFC2616 that suggests that we should. I do so for behaviour not
# security reasons.
my %gets =
  Inventory::populate_query_fields();    # not strictly a get field of course
my %POSTS;
if ( $ENV{REQUEST_METHOD} eq 'POST' ) {
    %POSTS = $q->Vars;
}

my $edit;
if ( $gets{'manufacturer_id'} && $gets{'manufacturer_id'} !~ m/\D/x ) {
    $edit = $gets{'manufacturer_id'};
}

my %message;    # communicate success or failure, sink or swim

# are we editing an existing entry?
if ( $POSTS{'manufacturer_id'} ) {
    %message =
      %{ Inventory::Manufacturers::edit_manufacturers( $dbh, \%POSTS ) };
}
elsif ( $POSTS{'manufacturer_name'} ) {

    # are we creating a new entry?
    %message =
      %{ Inventory::Manufacturers::create_manufacturers( $dbh, \%POSTS ) };
}

# list all the manufacturers (array of hashes)
my @manufacturers = Inventory::Manufacturers::get_manufacturers_info($dbh);

# number of models per manufacturer
my %model_count =
  %{ Inventory::Manufacturers::count_models_permanufacturer($dbh) };

# number of models per manufacturer
my %host_count =
  %{ Inventory::Manufacturers::count_hosts_permanufacturer($dbh) };

$tt->process(
    'inventory_manufacturers.tt',
    {
        manufacturers => \@manufacturers,
        message       => \%message,
        edit          => $edit,
        model_counts  => \%model_count,
        host_counts   => \%host_count,
    }
);
$tt->process( 'stdfoot.tt', {} );

__END__

=head1 NAME

=head1 VERSION

This documentation refers to  version 

=head1 USAGE

=head1 REQUIRED ARGUMENTS

=head1 OPTIONS

=head1 DESCRIPTION

=head1 DIAGNOSTICS

error messages

=head1 CONFIGURATION AND ENVIRONMENT

Configuration files used?

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

none known

=head1 BUGS AND LIMITATIONS

Report any found to <hostmaster@ox.ac.uk>

=head1 AUTHOR


, maintained by <hostmaster@ox.ac.uk>

=head1 LICENSE AND COPYRIGHT



(c) University of Oxford
