#!/usr/bin/perl 
#
# Name: photos
# Creator: Guy Edwards
# Created: 2008-07-28
# Description: Groups administration page for the inventory
#
# $Id: photos 3538 2012-02-10 15:39:37Z guy $
# $LastChangedBy: guy $
# $LastChangedDate: 2012-02-10 15:39:37 +0000 (Fri, 10 Feb 2012) $
# $LastChangedRevision: 3538 $
# $uid: nuP9CIihl8L9z8rkwx9ZdjeoGwJHRAX3MNovBpOk_JGU3 $
#

use warnings FATAL => 'all';
use strict;

use CGI;
use Config::Tiny;
use DBI;
use DBD::Pg;
use File::Basename;
use Template;

use Inventory qw(dbconnect populate_query_fields acl_checker);
use Inventory::Hosts qw(get_hosts_info);
use Inventory::Photos
  qw(create_photos edit_photos upload_photos get_photos_info);

my $BASEURL = CGI::url( -base => 1 );
my $HOSTNAME = $BASEURL;

# remove everything from the first . onwards
$HOSTNAME =~ s/\..*$//gx;

# remove everything from the start to the last /
$HOSTNAME =~ s/^.*\///gx;

my $CONFIG      = Config::Tiny->new();
my $CONFIG_FILE = "/srv/www/$HOSTNAME/lib/inventory.ini";
$CONFIG = Config::Tiny->read("$CONFIG_FILE");

my $dbhost      = $CONFIG->{'database'}->{db_host};
my $dbname      = $CONFIG->{'database'}->{db_name};
my $dbuser      = $CONFIG->{'database'}->{db_user};
my $dbpass      = $CONFIG->{'database'}->{db_pass};
my $dbh         = Inventory::dbconnect( $dbname, $dbuser, $dbhost, $dbpass );
my $upload_dir  = $CONFIG->{'locations'}->{photo_dir};
my $valid_users = $CONFIG->{'general'}->{allowed_users};
my $htmltemplatepath = $CONFIG->{'locations'}->{tt_dir};
my $website          = $CONFIG->{'locations'}->{website};
my $image_path       = $CONFIG->{'locations'}->{image_path};

#############################################################################
#                                     main                                  #
#############################################################################

my $q = CGI->new();
print $q->header();

# this just loads the default site settings
my $tt = Template->new( { INCLUDE_PATH => $htmltemplatepath, } );

# print the stock header (banner and title)
$tt->process(
    'inventory_header.tt',
    {
        pagetitle  => 'Photos of Hosts',
        baseurl    => $BASEURL,
        extramenu  => 'inventory',
        javascript => 'on',
    }
);

# invalid users get no further than the next line
Inventory::acl_checker( $tt, $valid_users );

# I differenciate between get and post, not everyone agrees with this but
# there is an RFC2616 that suggests that we should. I do so for behaviour not
# security reasons.
my %gets = populate_query_fields();    # not strictly a get field of course
my %POSTS;
if ( $ENV{REQUEST_METHOD} eq 'POST' ) {
    %POSTS = $q->Vars;
}

my $edit;

if ( exists $gets{'photo_id'} && $gets{'photo_id'} !~ m/\D/x ) {
    $edit = $gets{'photo_id'};
}

my %message;    # communicate success or failure, sink or swim

# are we editing an existing entry?
if ( $POSTS{'photo_id'} ) {
    %message = %{ Inventory::Photos::edit_photos( $dbh, \%POSTS ) };
}
elsif ( $POSTS{'photo_url'} ) {

    # are we creating a new entry?
    %message = %{ Inventory::Photos::create_photos( $dbh, \%POSTS ) };
}
elsif ( $POSTS{'photo_upload'} && $POSTS{'host_id'} ) {

    # minor logic to improve error messages
    if ( $POSTS{'host_id'} !~ m/\D/x ) {
        if (
            !Inventory::Photos::upload_photos(
                $dbh,     'photo',     '_upload', $upload_dir,
                $website, $image_path, \%POSTS
            )
          )
        {
            $message{'ERROR'} =
'Internal Error: The server isnt letting this page upload photos! Someone (programmer or server admin) has made an error';
        }
        else {
            $message{'SUCCESS'} =
              'The photo was uploaded to the server successfully';
        }
    }
    else {
        $message{'ERROR'} =
          'Internal Error: The host_id appears to have non numeric characters';
    }
}

# list all the photos (array of hashes)
my @photos = Inventory::Photos::get_photos_info($dbh);

# list all the hosts (array of hashes)
my @hosts = Inventory::Hosts::get_hosts_info($dbh);

$tt->process(
    'inventory_photos.tt',
    {
        photos  => \@photos,
        hosts   => \@hosts,
        message => \%message,
        edit    => $edit,
        host_id => $gets{'host_id'},
    }
);
$tt->process( 'stdfoot.tt', {} );

__END__

=head1 NAME

=head1 VERSION

This documentation refers to  version 

=head1 USAGE

=head1 REQUIRED ARGUMENTS

=head1 OPTIONS

=head1 DESCRIPTION

=head1 DIAGNOSTICS

error messages

=head1 CONFIGURATION AND ENVIRONMENT

Configuration files used?

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

none known

=head1 BUGS AND LIMITATIONS

Report any found to <guyjohnedwards@gmail.com>

=head1 AUTHOR

Guy Edwards, maintained by <guyjohnedwards@gmail.com>

=head1 LICENSE AND COPYRIGHT

Network Device Inventory - keep a database of devices to feed other systems
(such as monitoring software). Copyright 2007 University of Oxford

This program is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free Software
Foundation; either version 2 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.  See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program; if not, write to the Free Software Foundation, Inc., 51 Franklin
Street, Fifth Floor, Boston, MA 02110-1301 USA.

The University of Oxford disclaims all copyright interest in the program
`Inventory' written by Guy Edwards as agreed by Dr. Stuart Lee, Director of
Oxford University Computer Services.

Oliver Gorwits, who in 2008 contributed sections of the hostgroups programming
also disclaims all copyright interest in the program.
